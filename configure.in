dnl Process this file with autoconf to produce a configure script.
AC_INIT(postal.cpp)

dnl AM_CONFIG_HEADER(config.h)

AC_ARG_ENABLE(conversion,
      [  --enable-conversion          enables user-name conversion - default],
      CONVERSION=$enableval, CONVERSION=yes)

AC_SUBST(conv_objs)
AC_SUBST(conversion)
if [[ "$CONVERSION" = "yes" ]]; then
  conv_objs=expand.o
else
  conversion=["#define NO_CONVERSION"]
fi

AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_INSTALL

AC_SUBST(extra_ldflags)
extra_ldflags=
AC_ARG_WITH(extra-libs, [  --with-extra-libs=DIR   adds non standard library paths],
  use_extra_libs=$withval,
  use_extra_libs=NONE
)
if test -n "$use_extra_libs" && \
   test "$use_extra_libs" != "NONE"; then
 
   ac_save_ifs=$IFS
   IFS=':'
   for dir in $use_extra_libs; do
     extra_ldflags="$extra_ldflags -L$dir"
   done
   IFS=$ac_save_ifs
fi


dnl Checks for library -l socket.
AC_CHECK_LIB(socket, bind, extra_ldflags="$extra_ldflags -lsocket -lnsl")

dnl Checks for header files.
AC_SUBST(ssl)
AC_SUBST(extra_objs)
AC_CHECK_HEADER(openssl/crypto.h,
      ssl="#define USE_SSL" extra_ldflags="$extra_ldflags -lssl -lcrypto"
             , extra_objs="md5.o")

AC_SUBST(linux_pthread)
AC_TRY_COMPILE([#define _GNU_SOURCE
#include <pthread.h>
] , [pthread_mutexattr_t attr;
    pthread_mutexattr_settype(&attr, PTHREAD_MUTEX_RECURSIVE_NP);]
  , linux_pthread="yes")
if [[ -n "$linux_pthread" ]]; then
   linux_pthread="#define LINUX_PTHREAD"
fi

AC_SUBST(socklen_t)
AC_TRY_COMPILE([#include <sys/socket.h>
] , [socklen_t * namelen;], , no_socklen_t="true")
if [[ -n "$no_socklen_t" ]]; then
  socklen_t="#define socklen_t int"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.

AC_OUTPUT(Makefile postal.h)
